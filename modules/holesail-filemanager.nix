### Holesail filemanager module.  WORK IN PROGRESS ###############
{
  config,
  lib,
  pkgs,
  ...
}:
with lib;
let
  holesail = (import ../holesail.nix {inherit pkgs;});
  cfg = config.services.holesail-filemanager;
in
{
  options.services.holesail-filemanager = mkOption {
    type = types.attrsOf (types.submodule {
      options = {
        enable = mkEnableOption "Enable this Holesail filemanager instance.";
        host = mkOption {
          type = types.str;
          default = "127.0.0.1";
          description = "Host address to use for this Holesail filefilemanager instance.";
        };
        port = mkOption {
          type = types.port;
          default = 5409;
          description = "The port which this Holesail filemanager instance should use.";
        };
        udp = mkOption {
          type = types.bool;
          default = false;
          description = "Enable UDP instead of TCP.";
        };
        connector = mkOption {
          type = types.str;
          default = "";
          description = "The connection string of the Holesail filemanager. If empty, a random one will be generated by holesail.";
        };
        connector-file = mkOption {
          type = types.str;
          default = "";
          description = "The path to a file containing the connection string of the Holesail filemanager. If null, the connector option will be used.";
        };
        public = mkOption {
          type = types.bool;
          default = false;
          description = "Whether to announce the filemanager to the DHT.";
        };
        username = mkOption {
          type = types.str;
          default = "admin";
          description = "The username to use to login to the filemanager.";
        };
        password = mkOption {
          type = types.str;
          default = "admin";
          description = "The password to use to login to the filemanager.";
        };
        role = mkOption {
          type = types.str;
          default = "user";
          description = "The role to assign to the user. It can be 'admin' or 'user'.";
        };
        path = mkOption {
          type = types.str;
          default = "";
          description = "The path of the directory that will be served by the filemanager.";
        };
      };
    });
    description = "Configure multiple Holesail filemanager instances.";
    default = {};
  };

  config = mkIf (any (name: cfg.${name}.enable) (attrNames cfg)) {
    systemd.services = genAttrs (attrNames cfg) (name: 
      let
        instanceCfg = cfg.${name};
      in
        mkIf instanceCfg.enable {
          description = "Holesail filemanager (${name})";
          wantedBy = [ "multi-user.target" ];
          after = [ "network.target" ];
          path = [ holesail ];
          script = ''holesail --filemanager ${instanceCfg.path} \
              ${if instanceCfg.connector != "" then "--connector ${instanceCfg.connector}" else ""} \
              ${if instanceCfg.connector-file != "" then "--connector $(cat ${instanceCfg.connector-file})" else ""} \
              --port ${toString instanceCfg.port} \
              --host ${instanceCfg.host} \
              ${if instanceCfg.udp then "--udp" else ""} \
              ${if instanceCfg.public then "--public" else ""} \
              --username ${instanceCfg.username} \
              --password ${instanceCfg.password} \
              --role ${instanceCfg.role}
            '';
          serviceConfig.Type = "simple";
          serviceConfig.Restart = "always";
          serviceConfig.RestartSec = "10";
        }
    );
  };

  meta.maintainers = with maintainers; [ jjacke13 ];
}
