{ config
, lib
, pkgs
, ...
}:
with lib;
let
  holesail = pkgs.callPackage ../holesail.nix { };
  cfg = config.services.holesail-server;
in
{
  options.services.holesail-server = mkOption {
    type = types.attrsOf (types.submodule {
      options = {
        enable = mkEnableOption "Enable this Holesail server instance.";
        host = mkOption {
          type = types.str;
          default = "127.0.0.1";
          description = "Host address to use for this Holesail server instance.";
        };
        port = mkOption {
          type = types.port;
          default = 8989;
          description = "The port which this Holesail server instance should use.";
        };
        udp = mkOption {
          type = types.bool;
          default = false;
          description = "Enable UDP instead of TCP.";
        };
        connector = mkOption {
          type = types.str;
          default = "";
          description = "The connection string to the Holesail server. If empty, a random one will be generated by holesail.";
        };
        connector-file = mkOption {
          type = types.str;
          default = "";
          description = "The path to a file containing the connection string to the Holesail server. If null, the connection-string option will be used.";
        };
        public = mkOption {
          type = types.bool;
          default = false;
          description = "Whether to announce the server to the DHT. If true, connector must be empty.";
        };
      };
    });
    description = "Configure multiple Holesail client instances.";
    default = { };
  };

  config = mkIf (any (name: cfg.${name}.enable) (attrNames cfg)) {
    systemd.services = genAttrs (attrNames cfg) (name:
      let
        instanceCfg = cfg.${name};
      in
      mkIf instanceCfg.enable {
        description = "Holesail server (${name})";
        wantedBy = [ "multi-user.target" ];
        after = [ "network.target" ];
        path = [ holesail ];
        script = ''holesail \
              ${if instanceCfg.connector != "" then "--connector ${instanceCfg.connector}" else ""} \
              ${if instanceCfg.connector-file != "" then "--connector $(cat ${instanceCfg.connector-file})" else ""} \
              --live ${toString instanceCfg.port} \
              --host ${instanceCfg.host} \
              ${if instanceCfg.udp then "--udp" else ""} \
              ${if instanceCfg.public then "--public" else ""}
            '';
        serviceConfig.Type = "simple";
        serviceConfig.Restart = "always";
        serviceConfig.RestartSec = "10";
      }
    );
  };

  meta.maintainers = with maintainers; [ jjacke13 ];
}
