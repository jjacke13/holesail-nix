{
  config,
  lib,
  pkgs,
  ...
}:
with lib;
let
  holesail = (import ../holesail.nix {inherit pkgs;});
  cfg = config.services.holesail-server;
in
{
  options.services.holesail-server = mkOption {
    type = types.attrsOf (types.submodule {
      options = {
        enable = mkEnableOption "Enable this Holesail server instance.";
        
        user = mkOption {
          description = "User account under which holesail runs.";
          default = "holesail";
          type = types.str;
        };
        group = mkOption {
          description = "Group under which holesail runs.";
          default = "holesail";
          type = types.str;
        };
        host = mkOption {
          type = types.str;
          default = "127.0.0.1";
          description = "Host address to use for this Holesail server instance.";
        };
        port = mkOption {
          type = types.port;
          description = "The local port to expose over Holesail. This option is required.";
        };
        udp = mkOption {
          type = types.bool;
          default = false;
          description = "Enable UDP instead of TCP.";
        };
        key = mkOption {
          type = types.str;
          default = "";
          description = "The connection string of the Holesail server. In private mode it should start with 'hs://s000' If empty, a random one will be generated by holesail.";
        };
        key-file = mkOption {
          type = types.str;
          default = "";
          description = "The path to a file containing the connection string of the Holesail server. If null, the key option will be used.";
        };
        public = mkOption {
          type = types.bool;
          default = false;
          description = "Whether to announce the server to the DHT. If you want to use public mode with a fixed connection key, you should
                        use as key just a random string of exactly 32 characters";
        };
        log = mkOption {
          type = types.bool;
          default = false;
          description = "Whether to enable logs of holesail.";
        };
        key-output-file = mkOption {
          type = types.nullOr types.str;
          default = null;
          description = "Path to save the generated hs:// connection key. Useful for automation and capturing connection strings.";
        };
      };
    });
    description = "Configure multiple Holesail client instances.";
    default = {};
  };

  config = mkIf (any (name: cfg.${name}.enable) (attrNames cfg)) {
    systemd.services = genAttrs (attrNames cfg) (name: 
      let
        instanceCfg = cfg.${name};
      in
        mkIf instanceCfg.enable {
          description = "Holesail server (${name})";
          wantedBy = [ "multi-user.target" ];
          after = [ "network.target" ];
          path = [ holesail ];
          script = let
            args = lib.concatStringsSep " " (lib.filter (x: x != "") [
              (if instanceCfg.key != "" then "--key ${instanceCfg.key}" else "")
              (if instanceCfg.key-file != "" then "--key $(cat ${instanceCfg.key-file})" else "")
              "--live ${toString instanceCfg.port}"
              "--host ${instanceCfg.host}"
              (if instanceCfg.udp then "--udp" else "")
              (if instanceCfg.public then "--public" else "")
              (if instanceCfg.log then "--log" else "")
            ]);
          in ''
            holesail ${args}
          '';
          serviceConfig = {
            Type = "simple";
            Restart = "always";
            RestartSec = "10";
            User = instanceCfg.user;
            Group = instanceCfg.group;
          } // lib.optionalAttrs (instanceCfg.key-output-file != null) {
            ExecStartPost = "${pkgs.bash}/bin/bash -c 'sleep 4; ${pkgs.systemd}/bin/journalctl -u ${name}.service --since \"5 seconds ago\" --no-pager | grep -oE \"hs://[a-zA-Z0-9]+\" | head -1 > ${instanceCfg.key-output-file}'";
          };
        }
    );

    users.users = lib.mkIf (any (name: cfg.${name}.user == "holesail") (attrNames cfg)) {
      holesail = {
        isSystemUser = true;
        group = "holesail";
        home = "/var/lib/holesail";
      };
    };

    users.groups = lib.mkIf (any (name: cfg.${name}.group == "holesail") (attrNames cfg)) {
      holesail = { };
    };
  };

  meta.maintainers = with maintainers; [ jjacke13 ];
}
