### Holesail server module.  WORK IN PROGRESS ###############
{
  config,
  lib,
  pkgs ? import <nixpkgs> {},
  ...
}:
let
  holesail = (import ./holesail.nix {inherit pkgs;});
  cfg = config.services.holesail-server;
in
{
  options.services.holesail-server = lib.mkOption {
    type = lib.types.attrsOf (lib.types.submodule {
      options = {
        enable = lib.mkEnableOption "Enable this Holesail server instance.";
        host = lib.mkOption {
          type = lib.types.str;
          default = "127.0.0.1";
          description = "Host address to use for this Holesail server instance.";
        };
        port = lib.mkOption {
          type = lib.types.port;
          default = 8989;
          description = "The port which this Holesail server instance should use.";
        };
        udp = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Enable UDP instead of TCP.";
        };
        connector = lib.mkOption {
          type = lib.types.str;
          default = "";
          description = "The connection string to the Holesail server. If empty, a random one will be generated by holesail.";
        };
        public = lib.mkOption {
          type = lib.types.bool;
          default = false;
          description = "Whether to announce the server to the DHT. If true, connector must be empty.";
        };
      };
    });
    description = "Configure multiple Holesail client instances.";
    default = {};
  };

  config = lib.mkIf (lib.any (name: cfg.${name}.enable) (lib.attrNames cfg)) {
    systemd.services = lib.genAttrs (lib.attrNames cfg) (name: 
      let
        instanceCfg = cfg.${name};
      in
        lib.mkIf instanceCfg.enable {
          description = "Holesail server (${name})";
          wantedBy = [ "multi-user.target" ];
          after = [ "network.target" ];
          serviceConfig = {
            ExecStart = ''${holesail}/bin/holesail \
              ${if instanceCfg.connector == "" then "" else "--connector ${instanceCfg.connector}"} \
              --live ${toString instanceCfg.port} \
              --host ${instanceCfg.host} \
              ${if instanceCfg.udp then "--udp" else ""} \
              ${if instanceCfg.public then "--public" else ""}
            '';
            Type = "simple";
            Restart = "always";
            RestartSec = "10";
          };
        }
    );
  };

  meta.maintainers = with lib.maintainers; [ jjacke13 ];
}
